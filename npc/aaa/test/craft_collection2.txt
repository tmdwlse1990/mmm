//-------------- NPC คราฟของ more item ------------------
icecastle,202,204,5	script	Henry ช่างฝีมือ2	800,{

	mes "[Henry]";
	mes "เฮ้ พี่! อยากให้ฉันช่วยคราฟอะไรให้ไหม?";
	next;

	switch(select("Potion X","Weapon Y","Armor Z","Accessory A","ไม่เป็นไร")) {
		case 1: callsub S_CRAFT, 1, 2, 537,1, 569,1, 2339; end;     // ใช้ Red Potion x10 + Empty Bottle x3
		case 2: callsub S_CRAFT, 2, 2, 501,1, 531,1, 2339; end;    // item 7035 x5 + item 1010 x2
		case 3: callsub S_CRAFT, 3, 3, 502,1, 503,1, 504,1, 2339; end;// item 756 x4 + item 501 x2 + item 578 x1
		case 4: callsub S_CRAFT, 4, 1, 531,5, 2339; end;              // item 501 x5
		case 5: mes "[Henry]"; mes "ไว้คราวหน้า!"; close;
	}

end;

//------ ฟังก์ชันคราฟหลายวัตถุดิบ ------
S_CRAFT:
	.@slot = getarg(0);        // ช่องสูตร
	.@req_count = getarg(1);   // จำนวนวัตถุดิบที่ใช้
	.@arg_offset = 2;
	
	// สร้างตัวแปรอัตราความสำเร็จแยก
	switch(.@slot) {
		case 1: .@chance_var$ = "#CRAFT_ITEM1_CHANCE"; break;
		case 2: .@chance_var$ = "#CRAFT_ITEM2_CHANCE"; break;
		case 3: .@chance_var$ = "#CRAFT_ITEM3_CHANCE"; break;
		case 4: .@chance_var$ = "#CRAFT_ITEM4_CHANCE"; break;
	}

	.@success_rate = getd(.@chance_var$);
	if (.@success_rate == 0) setd(.@chance_var$, 30);
	.@success_rate = getd(.@chance_var$);

	mes "[Henry]";
	mes "การคราฟชิ้นนี้ต้องใช้:";
	for (.@i = 0; .@i < .@req_count; .@i++) {
		.@id = getarg(.@arg_offset + .@i * 2);
		.@cnt = getarg(.@arg_offset + .@i * 2 + 1);
		.@name$ = getitemname(.@id);
		mes " ^0000FF" + .@name$ + "^000000 จำนวน " + .@cnt + " ชิ้น";

		if (countitem(.@id) < .@cnt) {
			mes "^FF0000คุณมีวัตถุดิบไม่พอ!^000000";
			close;
		}
	}

	.@output_item = getarg(.@arg_offset + .@req_count * 2);
	.@output_name$ = getitemname(.@output_item);

	mes "โอกาสสำเร็จ: ^00FF00" + .@success_rate + "%^000000";
	next;
	mes "จะเริ่มคราฟเลยไหม?";
	if(select("คราฟเลย!:ยังก่อน") == 2) close;

	// ลบวัตถุดิบ
	for (.@i = 0; .@i < .@req_count; .@i++) {
		.@id = getarg(.@arg_offset + .@i * 2);
		.@cnt = getarg(.@arg_offset + .@i * 2 + 1);
		delitem .@id, .@cnt;
	}

	specialeffect EF_MAGICROD;
	progressbar_NPC "00CCFF", 3;

	if (rand(100) < .@success_rate) {
		specialeffect EF_REFINEOK;
		mes "[Henry]";
		mes "เยี่ยม! คราฟสำเร็จ!";
		mes "คุณได้รับ ^00FF00" + .@output_name$ + "^000000";
		getitem .@output_item, 1;
		setd(.@chance_var$, 30);
	} else {
		specialeffect EF_CLAYMORE;
		mes "[Henry]";
		mes "แย่จัง... ไม่สำเร็จแฮะ";
		mes "ครั้งหน้าจะง่ายขึ้นอีก ^FF0000+5%^000000";
		setd(.@chance_var$, .@success_rate + 5);
	}
	end;
	
}