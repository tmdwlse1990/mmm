prt_fild08,51,61,4	script	Spirit Stone	406,{

	// ต้องเคยคุยกับแท่นหินก่อน
	if (#summon_hint != 1) {
		mes "[Summon Boss]";
		mes "คุณไม่รู้ว่าจะใช้แท่นหินนี้อย่างไร...";
		mes "บางอย่างขาดหายไป... ลองหาคำใบ้จากที่ไหนสักแห่ง...";
		close;
	}

	// ป้องกันการเรียกซ้ำ
	if ($@boss_active) {
		mes "[Summon Boss]";
		mes "มีบางอย่างอันตรายยังหลงเหลืออยู่ในพื้นที่...";
		mes "โปรดจัดการก่อนทำพิธีใหม่อีกครั้ง";
		close;
	}

	.@item1 = 7951;
	.@item2 = 2005002;
	.@item3 = 2005003;
	.@item4 = 2005004; // ของชิ้นที่ 4

	// ตรวจสอบ cooldown
	if (gettimetick(2) < $@last_boss_summon + $@boss_cooldown) {
		mes "[Summon Boss]";
		mes "แท่นหินยังไม่พร้อมใช้งานในขณะนี้...";
		close;
	}

	if (countitem(.@item1) >= 1 && countitem(.@item2) >= 1 && countitem(.@item3) >= 1 && countitem(.@item4) >= 1) {
		mes "[Summon Boss]";
		mes "แท่นหินมีปฏิกิริยากับของในกระเป๋าคุณ...";
		next;
		mes "ไอเทมทั้ง 4 ชิ้นเกิดการสั่นพ้องกัน...";
		mes "จะวางไอเทมทั้งหมดลงบนแท่นหินหรือไม่?";
		if (select("ใช่:ไม่") == 1) {
			delitem .@item1, 1;
			delitem .@item2, 1;
			delitem .@item3, 1;
			delitem .@item4, 1;

			mes "แผ่นหินเกิดการสั่นไหวรุนแรง!!!";

			.@min_x = 80;
			.@max_x = 280;
			.@min_y = 145;
			.@max_y = 345;

			.@x = rand(.@min_x, .@max_x);
			.@y = rand(.@min_y, .@max_y);
			.@map$ = strnpcinfo(4);

			specialeffect EF_BOSSTELEPORT;
			specialeffect EF_SUMMONMONSTER;

			monster .@map$, .@x, .@y, "Fei-Chai", 22381, 1, 0, "boss_controller::OnBossDead";

			set $@boss_active, 1;
			donpcevent "boss_controller::OnAutoRemove";
			set $@last_boss_summon, gettimetick(2);
			set $@boss_cooldown, rand(240, 360); // 46 ชั่วโมง

			announce "ผู้เล่น " + strcharinfo(0) + " ได้ทำการปลุกเสกบอสออกมาแล้ว!!!", bc_all;
		} else {
			mes "ยกเลิกการเรียกบอส";
		}
	} else {
		mes "แท่นหินไม่เกิดปฏิกิริยาอะไร...";
	}
	close;
}

prt_fild08,57,61,4	script	M.Seer	471,{

	if (#summon_hint == 1) {
		mes "[Stone of Hint]";
	mes "เสียงกระซิบเบาๆ แว่วมา...";
	mes "'รวมของจากธรรมชาติทั้งสี่ วางบนแท่น จะปรากฏสิ่งที่ซ่อนอยู่...'";
	next;

	mes "[Stone of Hint]";
	mes "เสียงยังไม่จางหาย... และเริ่มกระซิบถึง 'ของทั้งสี่'...";
	next;

	mes "^9999FF ชิ้นที่หนึ่ง^000000";
	mes "'เจ้าหมูป่าสีทอง ซ่อนตัวอยู่ในป่าลึก'";
	mes "'มีพละกำลัง และความอึดเกินกว่าจะมองข้าม...'";
	next;

	mes "^9999FF ชิ้นที่สอง^000000";
	mes "'นกโบราณ ผู้มีอายุยืนยาว... เคลื่อนไหวว่องไวเกินสายตา'";
	mes "'เงาสะท้อนในอากาศคือคำเตือนของมัน'";
	next;

	mes "^9999FF ชิ้นที่สาม^000000";
	mes "'จ่าฝูงหมาป่า คำรามก้องในลมหนาว'";
	mes "'เหล่าลูกน้องไม่เคยห่างกาย ไม่เคยโดดเดี่ยว'";
	next;

	mes "^9999FF ชิ้นที่สี่^000000";
	mes "' ปิศาจยักษ์สีชมพู ที่ชอบบุกรุกเมืองมิดการ์ด '";
	mes "' ก่อกวน อลม่าน อลเวง... '";
	
	close;
	}

	mes "[Stone of Hint]";
	mes "เสียงกระซิบเบาๆ แว่วมา...";
	mes "'รวมของจากธรรมชาติทั้งสี่ วางบนแท่น จะปรากฏสิ่งที่ซ่อนอยู่...'";
	next;

	mes "[Stone of Hint]";
	mes "เสียงยังไม่จางหาย... และเริ่มกระซิบถึง 'ของทั้งสี่'...";
	next;

	mes "^9999FF? ชิ้นที่หนึ่ง^000000";
	mes "'เจ้าหมูป่าสีทอง ซ่อนตัวอยู่ในป่าลึก'";
	mes "'มีพละกำลัง และความอึดเกินกว่าจะมองข้าม...'";
	next;

	mes "^9999FF? ชิ้นที่สอง^000000";
	mes "'นกโบราณ ผู้มีอายุยืนยาว... เคลื่อนไหวว่องไวเกินสายตา'";
	mes "'เงาสะท้อนในอากาศคือคำเตือนของมัน'";
	next;

	mes "^9999FF? ชิ้นที่สาม^000000";
	mes "'จ่าฝูงหมาป่า คำรามก้องในลมหนาว'";
	mes "'เหล่าลูกน้องไม่เคยห่างกาย ไม่เคยโดดเดี่ยว'";
	next;

	mes "^9999FF? ชิ้นที่สี่^000000";
	mes "' ปิศาจยักษ์สีชมพู ที่ชอบบุกรุกเมืองมิดการ์ด '";
	mes "' ก่อกวน อลม่าน อลเวง... '";
	next;

	mes "[Stone of Hint]";
	mes "'เมื่อครบทั้งสี่ จงกลับมา...'";
	specialeffect EF_BEGINSPELL;
	set #summon_hint, 1;
	close;
}

-	script	boss_controller	-1,{

OnBossDead:
	announce "บอสถูกกำจัดโดย " + rid2name(killerrid) + "!", bc_all;
	set $@boss_active, 0;
	end;

OnAutoRemove:
	
	sleep 3600000; // 1 ชั่วโมง (60*60*1000 ms)
	if ($@boss_active) {
		killmonster "prt_fild08","boss_controller::OnBossDead";
		announce "บอสถูกกำจัดแล้ว!!!", bc_all;
		set $@boss_active, 0;
	}
	end;
}

icecastle,193,229,4	script	บอร์ดประชาสัมพันธ์	857,{

L_Page1:
	mes "[บอร์ดประชาสัมพันธ์]";
	mes "หน้า 1/3: เลือกประกาศเพื่ออ่านรายละเอียด";
	next;
	switch(select(
		"มีแมวพูดได้ใน Geffen",
		"ขายน้ำหอมทำจากตัว Orc",
		"ขอเช่าฮาโลวีนแมว 1 ตัว",
		"เยลลี่บุกเมือง!!",
		"ถัดไป ->",
		"เลิกดู"
	)) {
		case 1:
			mes "[ใบประกาศ]";
			mes "'มีคนเห็นแมวดำพูดได้เดินเข้าโรงแรมใน Geffen  แค่ข่าวลือ?'";
			close;
		case 2:
			mes "[ใบประกาศ]";
			mes "'ใครต้องการกลิ่นเฉพาะทาง ติดต่อแม่ค้าใต้สะพาน Prontera'";
			close;
		case 3:
			mes "[ใบประกาศ]";
			mes "'ตามหาคนปล่อยแมวผีในกิจกรรมปีที่แล้ว  อยากเช่ามาเล่น'";
			close;
		case 4:
			mes "[ใบประกาศ]";
			mes "'ไอ้เจ้าเยลลี่ยักษ์นั่น มันมากินขนมบ้านไปหมดเลย  กำจัดมันซะ '";
			next;
			mes "[เหมือนจะเคยได้ยินอยู่ว่าอยู่ในเมือง Prontera.....]";
			close;
		case 5:
			goto L_Page2;
		case 6:
			mes "[บอร์ดประชาสัมพันธ์]";
			mes "คุณละสายตาจากกระดาน...";
			close;
	}

L_Page2:
	mes "[บอร์ดประชาสัมพันธ์]";
	mes "หน้า 2/3: เลือกประกาศเพื่ออ่านรายละเอียด";
	next;
	switch(select(
		"ช่วยกำจัดแมลงสาบในท่อระบายน้ำ", 
		"ตามหาหมาป่าคำรามในป่าสน",         // จริง
		"ประกวดลุงใส่หมวกใน Alberta",
		"ขโมยขนมจากเด็กนักเรียน",
		"ถัดไป ->",
		"<- ย้อนกลับ",
		"เลิกดู"
	)) {
		case 1:
			mes "[ใบประกาศ]";
			mes "'ในท่อทางใต้มีแมลงสาบสีชมพูขนาดยักษ์ ช่วยกำจัดด่วน!'";
			close;
		case 2:
			mes "[ใบประกาศ]";
			mes "'หมาป่าลึกลับมีเสียงคำรามแปลก ๆ ปรากฏตัวในป่าสนทางใต้'";
			next;
			mes "ใช้หัวหน้าจ่าฝูงรุ่นใหม่ในป่า Payon รึเปล่านะ..";
			close;
		case 3:
			mes "[ใบประกาศ]";
			mes "'ใครมีลุงเท่ ๆ ใส่หมวก  เชิญประกวดเดือนหน้า'";
			close;
		case 4:
			mes "[ใบประกาศ]";
			mes "'มีคนขโมยขนมจากโรงเรียนแถว Payon  กรุณาช่วยสืบสวน'";
			close;
		case 5:
			goto L_Page3;
		case 6:
			goto L_Page1;
		case 7:
			mes "[บอร์ดประชาสัมพันธ์]";
			mes "คุณละสายตาจากกระดาน...";
			close;
	}

L_Page3:
	mes "[บอร์ดประชาสัมพันธ์]";
	mes "หน้า 3/3: เลือกประกาศเพื่ออ่านรายละเอียด";
	next;
	switch(select(
		"เงานกประหลาดในฟ้าเหนือทะเลทราย",  // จริง
		"หมูทองป่าลึกซ่อนของล้ำค่า",       // จริง
		"เสียงประหลาดจากท่อระบายน้ำ (อีกแล้ว)",
		"จ้างคนจับลูกเจี๊ยบบินได้",
		"<- ย้อนกลับ",
		"เลิกดู"
	)) {
		case 1:
			mes "[ใบประกาศ]";
			mes "'นกบินเร็วผิดปกติในกลางแสงแดดจ้า แต่หน้าตาดันแปลกกว่าชาวบ้าน...'";
			next;
			mes "เหมือนข้าจะเคยเห็นแถวๆก่อนไปป่าโปลิ่งเลย....";
			close;
		case 2:
			mes "[ใบประกาศ]";
			mes "'เจอหมูป่าสีทองในป่าทึบ มีคนบอกว่ามันเฝ้าขุมสมบัติ'";
			next;
			mes "นี่มัน.....ที่ผู้ใหญ่บ้านพูดถึงสินะ...เนื้อหอมๆ";
			close;
		case 3:
			mes "[ใบประกาศ]";
			mes "'ทุกคนได้ยินเสียง บรู๋ววว์  ไม่รู้แมลงสาบหรือคนเมา'";
			close;
		case 4:
			mes "[ใบประกาศ]";
			mes "'มีลูกเจี๊ยบบินหลุดออกจากห้องทดลอง  โปรดจับคืนมา!'";
			close;
		case 5:
			goto L_Page2;
		case 6:
			mes "[บอร์ดประชาสัมพันธ์]";
			mes "คุณละสายตาจากกระดาน...";
			close;
	}
}