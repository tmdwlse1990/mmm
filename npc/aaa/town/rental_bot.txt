icecastle,196,211,4	script	AutoBot	3155,{
	disable_items;
	mes "[หุ่นยนต์ AutoBots]";
	mes "บี๊บบ...ข้ามีบริการช่วยเล่นให้กับเจ้าทุกคน!";
	next;
	
	switch(select("1. รับฟรี 2 ชั่วโมง (วันละครั้ง):2. ซื้อบริการเช่า AI:3. ไม่มีอะไร")) {
		
		// 1. รับฟรี 2 ชั่วโมง
		case 1:
			// เช็คว่ามีของเช่าอยู่หรือไม่
			if (rentalcountitem(2002000)) {
				mes "[หุ่นยนต์ AutoBots]";
				mes "ปี๊บ.....ไอเทมเช่ายังอยู่! โปรดใช้ให้หมดก่อน.";
				close;
			}

			// ตรวจสอบ cooldown 24 ชั่วโมง (86400 วินาที)
			if (#rental_ai_free_cooldown == gettime(DT_YYYYMMDD)) {
				//set .@remain, #rental_ai_free_cooldown - gettimetick(2);
				//set .@hour, .@remain / 3600;
				//set .@min, (.@remain % 3600) / 60;
				mes "[หุ่นยนต์ AutoBots]";
				mes "คุณได้รับของฟรีไปแล้ววันนี้!";
				mes "กลับมาอีกครั้งในวันพรุ่งนี้.";
				//mes "กลับมาอีกครั้งในอีก..... " + .@hour + " ชม....... " + .@min + " นาที.";
				close;
			}

			// แจกของฟรี 2 ชั่วโมง
			set .@duration, 2 * 3600 + 120;
			rentitem 2002000, .@duration;
			//set #rental_ai_expire, gettimetick(2) + .@duration;
			//set #rental_ai_free_cooldown, gettimetick(2) + 86400;
			set #rental_ai_free_cooldown, gettime(DT_YYYYMMDD);

			mes "[หุ่นยนต์ AutoBots]";
			mes "รับ ^i[2002000] สำหรับเช่า 2 ชั่วโมงเรียบร้อย!";
			close;

		// 2. ซื้อบริการเช่า AI
		case 2:
			if (rentalcountitem(2002000) ) {
				mes "[หุ่นยนต์ AutoBots]";
				mes "ปี๊บ.....ไอเทมเช่ายังอยู่! โปรดใช้ให้หมดก่อน.";
				mes "หรืออยากต่ออายุ AI แต่รวมกันแล้วเวลาไม่ถึง 24 ชั่วโมงเหมือนเดิมนะ";
				if(select("ไม่เป็นไร:ต่ออายุ AI") == 2 ) goto ExtendAITime;
				close;
			}

			mes "[หุ่นยนต์ AutoBots]";
			mes "ค่าบริการอยู่ที่ 100,000 Zeny ต่อชั่วโมง";
			mes "ต้องการใช้บริการกี่ชั่วโมง? (1-24)";
			input .@hour;

			if (.@hour < 1 || .@hour > 24) {
				mes "กรุณาใส่จำนวนชั่วโมงระหว่าง 1 ถึง 24 เท่านั้น!";
				close;
			}

			set .@cost, .@hour * 100000;

			if (Zeny < .@cost) {
				mes "คุณมี Zeny ไม่เพียงพอ ต้องใช้ทั้งหมด " + .@cost + " Zeny.";
				close;
			}

			// หักเงิน
			set Zeny, Zeny - .@cost;

			// ให้ไอเทม
			set .@duration, .@hour * 3600 + 120;
			rentitem 2002000, .@duration;
			//set #rental_ai_expire, gettimetick(2) + .@duration;

			mes "เรียบร้อย! คุณเช่า บอทแมว สำหรับ " + .@hour + " ชั่วโมง.";
			close;

		// 3. ไม่มีอะไร
		case 3:
			mes "[หุ่นยนต์ AutoBots]";
			mes "ไว้เจอกันใหม่!.... ปี๊บบบ...";
			close;
	}

ExtendAITime:
	clear;
	if(!rentalcountitem(2002000)) {
		mes "[หุ่นยนต์ AutoBots]";
		mes "กรุณาใช้ AI ทีละตัวภายในไอดี";
		close;
	}
	mes "[หุ่นยนต์ AutoBots]";
	mes "ค่าบริการอยู่ที่ 100,000 Zeny ต่อชั่วโมง";
	set .@all_time,rentalcountitemtime(2002000); // Ai time left
	set .@cur_time,gettimetick(2);
	set .@dun_ent_t,(.@all_time - .@cur_time);
	set .@dun_h,(.@dun_ent_t / 3600);
	set .@dun_m,(.@dun_ent_t - (.@dun_h * 3600)) / 60;
	set .@dun_s,.@dun_ent_t - ((.@dun_h * 3600) + (.@dun_m * 60));
	set .@extentlimit, Max(0,23 - .@dun_h);
	mes "เวลาบอทที่เหลืออยู่ : " + .@dun_h + " ชั่วโมง " + .@dun_m + " นาที " + .@dun_s + " วินาที.";
	mes "สามารถต่อเวลาได้ทั้งหมด ^ff0000"+.@extentlimit+"^000000 ชั่วโมง";
	mes "อยากต่อเวลากี่ชั่วโมง ?";
	next;
	if(.@dun_h >= 23 ) {
		mes "[หุ่นยนต์ AutoBots]";
		mes "เวลายังเยอะอยู่ ต่อเวลาไม่ได้เน้อ";
		close;
	}
	setdialogsize(350,200);
	input .@hours;
	if(!.@hours) {
		mes "[หุ่นยนต์ AutoBots]";
		mes "โอกาสหน้าใช้บริการใหม่..... ปี๊บบบ";
		close;
	}
	if(.@hours > .@extentlimit) {
		mes "[หุ่นยนต์ AutoBots]";
		mes "เกินเวลาที่กำหนด..... ปี๊บบบ";
		close;
	}
	set .@cost, .@hours * 100000;
	mes "[หุ่นยนต์ AutoBots]";
	mes "จะต่ออายุ AI เป็น " + (.@dun_h + .@hours) + " ชั่วโมง.";
	mes "ค่าบริการเพิ่มเติมคือ :"+callfunc("F_InsertComma",.@cost)+" Zeny.";
	mes "ยืนยันหรือไม่ ?";
	next;
	if(select("  ยืนยัน:  ยังก่อน") == 2 ) end;
	 	// หักเงิน
	 if (Zeny < .@cost) {
		mes "คุณมี Zeny ไม่เพียงพอ ต้องใช้ทั้งหมด " + .@cost + " Zeny.";
		close;
	}
	set Zeny, Zeny - .@cost;

	// ให้ไอเทม
	delitem 2002000,1;
	set .@duration, .@hours * 3600 + .@dun_ent_t;
	//set #rental_ai_expire, gettimetick(2) + .@duration;
	rentitem 2002000, .@duration;
	mes "[หุ่นยนต์ AutoBots]";
	mes "เรียบร้อย! คุณเช่า บอทแมว เพิ่ม " + .@hours + " ชั่วโมง.";
	mes "เวลาบอทที่เหลืออยู่ : " + (.@dun_h + .@hours) + " ชั่วโมง " + .@dun_m + " นาที " + .@dun_s + " วินาที.";
	end;
	
	Oninit:
	waitingroom "Auto AI",0;
	end;
}