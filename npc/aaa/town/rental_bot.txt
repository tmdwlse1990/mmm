icecastle,196,211,4	script	AutoBot	3155,{

	mes "[หุ่นยนต์ AutoBots]";
	mes "บี๊บบ...ข้ามีบริการช่วยเล่นให้กับเจ้าทุกคน!";
	next;

	switch(select("1. รับฟรี 2 ชั่วโมง (วันละครั้ง):2. ซื้อบริการเช่า AI:3. ไม่มีอะไร")) {
		
		// 1. รับฟรี 2 ชั่วโมง
		case 1:
			// เช็คว่ามีของเช่าอยู่หรือไม่
			if (#rental_ai_expire > gettimetick(2)) {
				mes "[หุ่นยนต์ AutoBots]";
				mes "ปี๊บ.....ไอเทมเช่ายังอยู่! โปรดใช้ให้หมดก่อน.";
				close;
			}

			// ตรวจสอบ cooldown 24 ชั่วโมง (86400 วินาที)
			if (#rental_ai_free_cooldown > gettimetick(2)) {
				set .@remain, #rental_ai_free_cooldown - gettimetick(2);
				set .@hour, .@remain / 3600;
				set .@min, (.@remain % 3600) / 60;
				mes "[หุ่นยนต์ AutoBots]";
				mes "คุณได้รับของฟรีไปแล้ววันนี้!";
				mes "กลับมาอีกครั้งในอีก..... " + .@hour + " ชม....... " + .@min + " นาที.";
				close;
			}

			// แจกของฟรี 2 ชั่วโมง
			set .@duration, 2 * 3600 + 120;
			rentitem 2002000, .@duration;
			set #rental_ai_expire, gettimetick(2) + .@duration;
			set #rental_ai_free_cooldown, gettimetick(2) + 86400;

			mes "[หุ่นยนต์ AutoBots]";
			mes "รับ ^i[2002000] สำหรับเช่า 2 ชั่วโมงเรียบร้อย!";
			close;

		// 2. ซื้อบริการเช่า AI
		case 2:
			if (#rental_ai_expire > gettimetick(2)) {
				mes "[หุ่นยนต์ AutoBots]";
				mes "ปี๊บ.....ไอเทมเช่ายังอยู่! โปรดใช้ให้หมดก่อน.";
				close;
			}

			mes "[หุ่นยนต์ AutoBots]";
			mes "ค่าบริการอยู่ที่ 100,000 Zeny ต่อชั่วโมง";
			mes "ต้องการใช้บริการกี่ชั่วโมง? (1-24)";
			input .@hour;

			if (.@hour < 1 || .@hour > 24) {
				mes "กรุณาใส่จำนวนชั่วโมงระหว่าง 1 ถึง 24 เท่านั้น!";
				close;
			}

			set .@cost, .@hour * 100000;

			if (Zeny < .@cost) {
				mes "คุณมี Zeny ไม่เพียงพอ ต้องใช้ทั้งหมด " + .@cost + " Zeny.";
				close;
			}

			// หักเงิน
			set Zeny, Zeny - .@cost;

			// ให้ไอเทม
			set .@duration, .@hour * 3600 + 120;
			rentitem 2002000, .@duration;
			set #rental_ai_expire, gettimetick(2) + .@duration;

			mes "เรียบร้อย! คุณเช่า Knife สำหรับ " + .@hour + " ชั่วโมง.";
			close;

		// 3. ไม่มีอะไร
		case 3:
			mes "[หุ่นยนต์ AutoBots]";
			mes "ไว้เจอกันใหม่!.... ปี๊บบบ...";
			close;
	}
	Oninit:
	waitingroom "Auto AI",0;
	end;
}