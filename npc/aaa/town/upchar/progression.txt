icecastle,209,170,4	script	อัพเกรดบัตรนักผจญภัย	4_EP21_HEINE_TAB,{

	mes "";
	mes "_oo0oo_";
	mes "o8888888o";
	mes "88\" . \"88";
	mes "(| -_- |)";
	mes "0\\  =  /0";
	mes "___/---'\\___";
	mes ".' \\\\|     |// '.";
	mes "/ \\\\|||  :  |||// \\ ";
	mes "/ _||||| -:- |||||- \\ ";
	mes "|   | \\\\\\  -  /// |   |";
	mes "| \\_|  ''\\---/''  |_/ |";
	mes "\\  .-\\__  '-'  ___/-. /";
	mes "___'. .'  /--.--\\  . .'___";
	mes "\"\" '<  .___\\_<|>_/___.' >' \"\".";
	mes "| | :  - \\.;\\ _ /;./ -  : | |";
	mes "\\  \\ _.   \\_ __\\ /__ _/   .- /  /";
	mes "=====-.____.___ \\_____/___.-___.-'=====";
	mes "=---='";
	mes " ^i[513] ^i[512] ";
	mes "\\ ^i[514] ^i[2000001] ^i[619] /";
	mes "บูชาพระพุทธ  ??????  ก่อนเรียนด้วยนะ";
	mes "อยากจะสมัครเรียนคราสไหน ?";
	
	setdialogalign(DIALOG_ALIGN_MIDDLE);
	setdialogalign(DIALOG_ALIGN_CENTER);
	setdialogsize(600,700);
	next;

	// ตรวจสอบว่ามีบัตรนักผจญภัยใส่อยู่ไหม
	if(getequipid(EQI_SHADOW_ACC_L) != 2000001) {
		mes "ไหนหละบัตรนักผจญภัยของท่าน";
		mes "^i[2000001] อันนี้";
		mes "ถ้ามีกรุณาสวมใส่ด้วย";
		end;
	}

	// ถ้ายังไม่ได้ผ่านเควสเลื่อน Rank E ให้ reset adv_clas เป็น 0
	if (adv_clas >= 1 && !checkquest(81,HUNTING) && !checkquest(81,COMPLETE)) {
		dispbottom "Downgrade adv_clas = 0";
		adv_clas = 0;
	}

	// กำหนดค่าใช้จ่ายและตัวคูณตาม Rank
	switch(adv_clas) {
		case 0:	// F Class
			setarray .@zeny,10,50,100,300,500,800,1000,1250,1500,2000;
			.@multiple = 1000;
			break;
		case 1:	// E Class
			setarray .@zeny,1000,2000,3000,4000,5000,6000,7000,8000,9000,10000;
			.@multiple = 1000;
			break;
		case 2:	// D Class
			setarray .@zeny,11,12,13,14,15,16,17,18,19,20;
			.@multiple = 1000000;
			break;
		case 3:	// C Class
			setarray .@zeny,22,24,26,28,30,32,34,36,38,40;
			.@multiple = 1000000;
			break;
		case 4:	// B Class
			setarray .@zeny,44,48,52,56,60,64,68,72,76,80;
			.@multiple = 1000000;
			break;
		case 5:	// A Class
			setarray .@zeny,100,110,120,130,140,150,160,170,180,190;
			.@multiple = 1000000;
			break;
		default:
			mes "เกิดข้อผิดพลาด adv_clas ไม่ถูกต้อง";
			end;
	}

	// ชื่อ Rank
	setarray .@rankname$[0],"F","E","D","C","B","A";
	
	// จำนวนขั้นสูงสุดที่อัพได้ต่อ Rank (10 ขั้น)
	.@max_level = 10;
	
	// เมนูเลือก stat ที่จะอัพ
	.@menu$ = "";
	for(.@i = 0; .@i < getarraysize($class$); .@i++) {
		
		.@total_check = getd($stats$[.@i]);
		.@cur_check = .@total_check - (adv_clas * .@max_level);
		if(.@cur_check < .@max_level)
			.@menu$ += $class$[.@i] + ":";
		else
			.@menu$ += ":";
	}
	.@menu = select(.@menu$) - 1;

	

	// ค่า stat รวมสะสมทั้งหมด (ทุก Rank รวมกัน)
	.@total_stat = getd($stats$[.@menu]);

	// ขั้นตอนปัจจุบันของ Rank นี้ (ลบขั้นตอนทั้งหมดของ Rank ก่อนหน้าออก)
	.@cur_stat = .@total_stat - (adv_clas * .@max_level);

	// ถ้าอัพเกรดครบขั้นสูงสุดแล้ว (เท่ากับหรือมากกว่า max_level)
	if (.@cur_stat >= .@max_level) {
		mes "ท่านอัพถึงระดับสูงสุดของ Rank " + .@rankname$[adv_clas] + " ในด้านนี้แล้ว";
		mes "ท่านต้องผ่านการทดสอบของทางกิล เพื่อทำการเลื่อนระดับแรงค์";
		end;
	}

	// ป้องกันกรณี .@cur_stat เกินขนาด array ค่าใช้จ่าย
	if (.@cur_stat >= getarraysize(.@zeny)) {
		.@cur_stat = getarraysize(.@zeny) - 1;
	}

	// คำนวณค่าใช้จ่าย Zeny ตามขั้นตอนปัจจุบัน
	.@usezeny = .@zeny[.@cur_stat] * .@multiple;

	// ตรวจสอบว่ามีเงินพอไหม
	if (zeny < .@usezeny) {
		mes "ตังไม่พอนะ";
		mes "มันต้องใช้ตัง " + callfunc("F_InsertComma", .@usezeny) + " Zeny";
		mes "ซึ่งนายขาดอยู่ " + callfunc("F_InsertComma", (.@usezeny - zeny));
		end;
	}

	// ยืนยันการใช้เงิน
	mes "มันต้องใช้ตัง " + callfunc("F_InsertComma", .@usezeny) + " Zeny";
	mes "เอาเลยไหม?";
	if(select("เอาเลย:ยังก่อน") == 2) end;

	// หักเงินและเพิ่ม stat ขั้นตอน (เพิ่ม 1 ขั้นตอนในรวมทั้งหมด)
	zeny -= .@usezeny;
	setd($stats$[.@menu], .@total_stat + 1);

	mes "เรียบร้อย แถมบัพให้ด้วย!";
	specialeffect2 EF_INCAGILITY;
	sc_start SC_INCREASEAGI, 600000, 10;
	specialeffect2 EF_BLESSING;
	sc_start SC_BLESSING, 600000, 10;

	if(getequipid(EQI_SHADOW_ACC_L) > 0) unequip EQI_SHADOW_ACC_L;
	equip 2000001;
	end;

OnInit:
	deletearray $stats$[0], getarraysize($stats$);
	deletearray $class$[0], getarraysize($class$);
	setarray $stats$, "adv_atk", "adv_matk", "adv_def", "adv_mdef", "adv_hit", "adv_flee", "adv_hp", "adv_sp", "adv_igdef", "adv_igmdef";
	setarray $class$, "Atk", "Matk", "Def", "Mdef", "Hit", "Flee", "Max HP", "Max SP", "Ignore Def", "Ignore MDef";
	end;
}

icecastle,201,171,4	script	พนักงานต้อนรับ	4_EP21_GALAXIA_B,{
	cutin "ep21_galaxia01.png",2;
	setarray .@rankname$[0],"F","E","D","C","B","A","S";
	mes "สวัสดีนักพจญภัย ท่านต้องการบริการด้านใดค่ะ";
	next;
	if(#newbietutorial == 2) {
		if(!adv_atk) {
			mes "อ๊ะ ท่านคือผู้ที่มาใหม่สินะ";
			mes "สวัสดีจ้า นี่คือกิลนักพจญภัย";
			mes "นายสามารถอัพเกรดบัตร ^i[2000001]";
			mes "ผ่านตัวอัพเกรด Card Upgrade ได้นะ";
			mes "นายลองอัพ Atk ดูซักขั้นสิ แล้วมาคุยกับฉันใหม่นะ";
			if(!#learnupcard) {
				mes "ฉันจะให้เงินไปอัพขั้นแรกนะ.";
				next;
				mes "^ff0000 อัพ ATK นะอย่าพึ่งอัพอันอื่น^000000";
				zeny += 10000;
				#learnupcard += 1;
			}
			else {
				mes "^ff0000 อัพ ATK นะอย่าพึ่งอัพอันอื่น^000000";
			}
			cutin "",255;
			end;
		}
		else if(adv_atk > 1) {
			mes "นายอัพไปเยอะแล้วนี่น่า เอาตัวใหม่มาเรียนรู้แทนนะ";
			end;
		}
		else {
			mes "โอ้เยี่ยมไปเลย";
			mes "นี่คือวิธีการอัพขั้นบัตร Adventure Card เบื้องต้น";
			mes "ทุกการอัพเกรดใช้ Zeny และมันมีระดับ Rank ด้วยนะ";
			mes "นายจะสามารถอัพ Rank ได้ก็ต่อเมื่อนายอัพทุกหัวข้อเต็ม 10 ระดับแล้วเท่านั้น";
			mes "ถือเป็นของรางวัลเริ่มต้น ฉันจะให้แต่ละขั้นของนายไว้ที่ขั้น 5 นะ <3";
			next;
			for(.@i=0 ;.@i < getarraysize($stats$); .@i++)
				setd($stats$[.@i], 5);
			mes "ไปที่ต่อไปกันเลยที่ ^ff0000Pet Manager^000000";
			navigateto("icecastle",208,158);
			#newbietutorial = 3;
			cutin "",255;
			end;
		}
	}
	switch(select("ตรวจสอบข้อมูลการอัพเกรด:ตรวจสอบค่าใช้จ่ายแต่ละระดับ:ติดต่อกิลมาสเตอร์:ไม่มีอะไรมาดูหน้าเฉยๆ") ) {
		case 1:

			mes "ปัจจุบันของท่านได้รับความสามารถตามนี้";
			if(adv_clas) {
				mes "Rank :"+.@rankname$[adv_clas]+" Bonus";
				mes "ATK/MATK : "+(3 * adv_clas)+"%";
				mes "HP/SP : "+(2 * adv_clas)+"%";
				mes "Ignore Def/Mdef : "+(adv_clas)+"%";
				mes "ได้รับ Skill Greed Lv: "+(adv_clas);
			}
			mes "--------------------------------";
			for(.@i=0;.@i <= 1; .@i++)
				mes ""+$class$[.@i]+" : "+getd($stats$[.@i])*10+" หน่วย.";
			for(.@i=2;.@i <= 5; .@i++)
				mes ""+$class$[.@i]+" : "+getd($stats$[.@i])*3+" หน่วย.";
			for(.@i=6;.@i <= 6; .@i++)
				mes ""+$class$[.@i]+" : "+getd($stats$[.@i])*100+" หน่วย.";
			for(.@i=7;.@i <= 7; .@i++)
				mes ""+$class$[.@i]+" : "+getd($stats$[.@i])*25+" หน่วย.";
			for(.@i=8;.@i <= 9; .@i++)
				mes ""+$class$[.@i]+" : "+getd($stats$[.@i])+" หน่วย.";
			mes "--------------------------------";
			mes "ตามนี้";
			setdialogsize(400,500);
			next;
		break;
		case 2:
			mes "ตามระดับของท่านปัจจุบันแล้ว";
			mes "ปัจจุบันมีแต่การอัพระดับของคราส "+.@rankname$[adv_clas]+" ขึ้นเป็นคราส "+.@rankname$[adv_clas+1]+"";
			switch(adv_clas) {
				case 0:	//[F Class]
					setarray .@zeny,10,50,100,300,500,800,1000,1250,1500,2000;
					.@multiple = 1000;
					break;
				case 1:	//[E Class]
					setarray .@zeny,1000,2000,3000,4000,5000,6000,7000,8000,9000,10000;
					.@multiple = 1000;
					break;
				case 2:	//[D Class]
					setarray .@zeny,11,12,13,14,15,16,17,18,19,20;
					.@multiple = 1000000;
					break;
				case 3:	//[C Class]
					setarray .@zeny,22,24,26,28,30,32,34,36,38,40;
					.@multiple = 1000000;
					break;
				case 4:	//[B Class]
					setarray .@zeny,44,48,52,56,60,64,68,72,76,80;
					.@multiple = 1000000;
					break;
				case 5:	//[A Class]
					setarray .@zeny,100,110,120,130,140,150,160,170,180,190;
					.@multiple = 1000000;
					break;
			}
			for(.@i = 0; .@i < getarraysize(.@zeny); .@i++)
				mes ""+(.@i+1)+". "+callfunc("F_InsertComma",(.@zeny[.@i] * .@multiple))+" Zeny";
			mes "ตามนี้";
			setdialogsize(400,500);
		break;
		case 3:
			warp "icas_in",108,54;
			close;
		break;
		default:
			mes "จ้า สู้ๆนะ";
			cutin "ep21_galaxia03.png",2;
		break;
	}
	cutin "",255;
	end;
Oninit:
	end;
}

//	มอนสเตอร์ อัพ Rank
pay_fild04,201,178	boss_monster	King Poring	3810,1,10800000,600000		// E Rank
// =================

icas_in,108,60,4	script	Guild Master#prog	22180,{
	function RGBToHex;
	setarray .@rankname$[0],"F","E","D","C","B","A";
	.@npcname$ = "[ Master Of Shalarm ]";
	mes .@npcname$;
	for(.@i =0; .@i < getarraysize(.@rankname$); .@i++) {
		.@questid = 81+.@i;
		if( checkquest(.@questid,HUNTING) >= 2 ) {
			mes "ทำได้ดี จะเลื่อนขั้นให้ก็แล้วกัน";
			specialeffect2 8;
			specialeffect2 12;
			specialeffect2 17;
			completequest .@questid;
			adv_clas++;
			end;
		}
	}
	mes "ธุระคือ?";
	next;
	if(select("- ขอเลื่อนคราสหน่อยครับ:- ไม่มีอะไรครับ") == 2 ) { warp "Save",0,0; end; }
	.@checklvl = adv_clas;
	mes .@npcname$;
	mes "จะเลือนแรงค์ตัวเองหรอ เจ้านี่อยู่แค่แรงค์ " +.@rankname$[.@checklvl]+" "+ (.@checklvl < 5 ? "เองหรอ.":"แล้วหรอเนี่ย โตขึ้นเยอะนี่.");
	mes "อยากทำสอบเลยไหมหละ ?";
	next;
	if(select("- พร้อมครับ:- ยังไม่พร้อมเลยครับ") == 2 ) { warp "Save",0,0; end; }

	mes .@npcname$;
	for(.@i = 0; .@i < getarraysize($stats$); .@i++) {
		.@checkstats = (getd($stats$[.@i]) >= 10 * (.@checklvl+1) ? getd($stats$[.@i]) : getd($stats$[.@i]) % 10);
		if(.@checkstats < 10 * (.@checklvl+1) ) {
			@notenought++;	
			do {
				// สุ่มค่า RGB
				set .@r, rand(0, 255);
				set .@g, rand(0, 255);
				set .@b, rand(0, 255);

				// คำนวณ brightness แบบใกล้เคียง (ใช้ int ทั้งหมด)
				set .@brightness, (.@r * 299 + .@g * 587 + .@b * 114) / 1000;
			} while (.@brightness > 127); // เฉพาะโทนเข้ม (brightness <= 127)
			// เรียกฟังก์ชันแปลงเป็น HEX สี HTML
				set .@color$, RGBToHex(.@r, .@g, .@b);
			mes "ดูเหมือนว่าค่าสถานะ ^"+ .@color$ + $class$[.@i]+"^000000 ยังไม่เก่งพอ^FF0000(ขาด "+((10 * (.@checklvl+1)) - getd($stats$[.@i]))+ " ขั้น)^000000";
		}
	}
	if(@notenought) {setdialogsize(400,500); end;}
	
	switch (.@checklvl) {
		case 0:
			if(!isbegin_quest(81)) setquest 81;
			mes "ไปที่ป่าโปริ่งสิ ไปนำสมบัติของ King มาให้ข้า";
			mes "ถ้าทำได้หละนะ";
			break;
		case 1:
			if(!isbegin_quest(82)) setquest 82;
			mes "ไหนลองไปล่าเจ้าหนวดหมึกย่างนั่นมาซัก 200 ตัวหน่อย";
			mes "ถ้าทำได้หละนะ";
			break;
		case 2:
			if(!isbegin_quest(83)) setquest 83;
			mes "Rank นี้คนบาดเจ็บเยอะมากๆจากป่าพิษ ไปจัดการเจ้าตัวปล่อยพิษที";
			mes "ถ้าทำได้หละนะ";
			break;
		case 3:
		case 4:
		case 5:
			mes .@npcname$;
			mes "พักไปก่อน ยังไม่มีคำขอมาตอนนี้";
		break;
		default:
			mes "เกิดข้อผิดพลาด";
			mes "กรุณาติดต่อ Admin";
			end;
		break;
	}
end;


	// ฟังก์ชันแปลง RGB เป็น HEX
	function RGBToHex {
		function Hex2;

		set .@r$, Hex2(getarg(0));
		set .@g$, Hex2(getarg(1));
		set .@b$, Hex2(getarg(2));

		return .@r$ + .@g$ + .@b$;
	}

	// แปลงเลข 0255 เป็น HEX 2 หลัก
	function Hex2 {
		setarray .@hex$[0], "0","1","2","3","4","5","6","7","8","9","A","B","C","D","E","F";
		set .@value, getarg(0);
		set .@hi, .@value / 16;
		set .@lo, .@value % 16;
		return .@hex$[.@hi] + .@hex$[.@lo];
	}
	
}
