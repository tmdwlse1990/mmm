icecastle,188,221,4	script	Chief Baron	EP18_NPC_SUAD,{

	if (MQ == 1) {
		mes "[ผู้ใหญ่บ้าน]";
		mes "อ้าว... มาหาผมเหรอ?";
		emotion e_ho; // ยิ้มต้อนรับ
		mes "ถ้าอยากได้ความช่วยเหลือจากหมู่บ้านนี้...";
		mes "ก่อนอื่น... จะต้องพิสูจน์ตัวเองก่อน!";
		emotion e_no1; // จริงจัง
		next;
		mes "[ผู้ใหญ่บ้าน]";
		mes "ไปหาหางหนูมาให้ผม 30 ชิ้น!";
		mes "แล้วผมจะพิจารณาว่าจะให้ความช่วยเหลือเธอหรือเปล่า";
		next;
		mes "[ผู้ใหญ่บ้าน]";
		mes "หางหนู 30 ชิ้นนะ! ถ้าไม่ครบก็ไม่มีอะไรให้หรอก!";
		set MQ, 2;
		close;
	}

	else if (MQ == 2) {
		mes "[ผู้ใหญ่บ้าน]";
		mes "เธอไปหาหางหนูมาแล้วเหรอ?";
		next;
		if (countitem(1016) >= 30) {
			mes "[ผู้ใหญ่บ้าน]";
			mes "ดีมาก! เอาหางหนูมาแล้วเหรอ?";
			mes "เอาล่ะ... แค่นี้ก็มีกับแกล้มเหล้าไปอีกพักใหญ่ๆละ!";
			specialeffect EF_CASH_BLUE; // เอฟเฟกต์เล่นขำๆ
			delitem 1016, 30;
			next;
			mes "[ผู้ใหญ่บ้าน]";
			mes "........!";
			emotion e_hmm;
			next;
			mes "เจ้ายังอยู่หรอ.... อ่อ รางวัลสินะ เอาอันนี้ไปละกัน ละไปไหนก็ไป";
			mes "ไว้มีงานอะไรให้ช่วยละเดะจะบอกเจ้าใหม่นะ!!";
			getitem 23503,15;
			getitem 23304,1;
			set MQ, 3;
			close;
		} else {
			mes "[ผู้ใหญ่บ้าน]";
			mes "เธอยังไม่ได้หางหนูครบ 30 ชิ้น!";
			emotion e_no;
			mes "ไปล่ามาให้ครบแล้วค่อยกลับมาหาผม!";
			close;
		}
	}

	else if (MQ == 3) {
		if (readparam(11) < 35) {
			mes "[ผู้ใหญ่บ้าน]";
			mes "ว่างายยยย มีไรให้ช่วยไหมเจ้าหนู";
			emotion e_omg;
			mes "ข้ายังไม่มีงานให้เจ้าทำหรอกนะ! ว่าแต่มีกับแกล้มเพิ่มไหม???";
			emotion e_cash;
			close;
		} else {
			mes "[ผู้ใหญ่บ้าน]";
			mes "อ่า... ถึงเวลานั้นแล้วเหรอ!";
			emotion e_gasp;
			mes "เทพผู้พิทักษ์หมู่บ้านอยากกินเนื้อหมู!";
			mes "แต่มันต้องเป็น... หมูป่าทองคำ! หายากมากๆนะ!";
			next;
			mes "[ผู้ใหญ่บ้าน]";
			mes "หมูป่าทองคำมันไม่ได้อยู่แถวนี้หรอก อยู่ในป่าอีกเมืองนึง...";
			emotion e_swt;
			mes "แต่จะล่ามันไม่ง่ายๆ หรอก!";
			next;
			mes "[ผู้ใหญ่บ้าน]";
			mes "ถ้าเธอล่ามันได้... จะมีรางวัลให้แน่นอน!";
			set MQ, 4;
			close;
		}
	}

	else if (MQ == 4) {
		mes "[ผู้ใหญ่บ้าน]";
		mes "เจ้าได้เนื้อหมูป่าทองคำมารึยัง!!!";
		emotion e_what;
		mes "ข้า.... เอ๊ย เทพผู้พิทักษ์หิวจนไส้กิ่วหมดละเนี่ยยย!!";
		next;
		if (countitem(6249) >= 1) {
			mes "[ผู้ใหญ่บ้าน]";
			mes "โอ้! ดูมันซิ เนื้อที่มีความมันขนาดนี้!?! ชิ้นใหญ่ขนาดนี้!!!";
			emotion e_ho;
			mes "ตะ แต่ว่า....กลิ่นแรงชะมัดดด!!";
			emotion e_swt;
			next;
			mes "[ผู้ใหญ่บ้าน]";
			mes "เหมือนมันจะขาดอะไรไปแน่ๆเลย....";
			mes "ใช่แล้ว! สมุนไพรดับกลิ่นสุดแรงนี่แหละที่ฉันต้องการ!";
			next;
			mes "[ผู้ใหญ่บ้าน]";
			mes "ไปหาสมุนไพรสีแดงๆ กับสีเขียวๆมาให้ข้าอย่างละ 5 ชิ้นนะ!";
			mes "แล้วค่อยนำมันมาให้ข้าปรุง!!";
			set MQ, 5;
			close;
		} else {
			mes "[ผู้ใหญ่บ้าน]";
			mes "ไหนหละ!! เนื้อหมูป่าทองคำที่น่าอร่อยของข้า!! เอ็ยของท่านเทพ!!";
			emotion e_omg;
			close;
		}
	}

	else if (MQ == 5) {
		if (countitem(507) >= 5 && countitem(511) >= 5 && countitem(6249) >= 1) {
			mes "[ผู้ใหญ่บ้าน]";
			mes "ดีมาก! เอาสมุนไพรที่หามาให้ข้า!";
			delitem 507, 5;
			delitem 511, 5;
			delitem 6249, 1;
			next;
			mes "[ผู้ใหญ่บ้าน]";
			mes "รอสักครู่...";
			mes "ได้เวลาแสดงฝีมือของข้าแล้วสินะ!!";
			specialeffect EF_STORMKICK4;
			progressbar_NPC "FF0000",3;
			next;
			mes "[ผู้ใหญ่บ้าน]";
			mes "เยี่ยม! ตอนนี้ฉันมีทุกอย่างแล้ว!";
			emotion e_gg;
			next;
			mes "[ผู้ใหญ่บ้าน]";
			mes "นี่รางวัลที่เธอสมควรได้รับ!";
			mes "แต่ข้าไม่แบ่งเนื้อของข้า เอ๊ย ของท่านเทพให้หรอกนะ!!";
			getitem 2280,1;
			getitem 12495,2;
			getitem 12497,2;
			set MQ,6;
			close;
		} else {
			mes "[ผู้ใหญ่บ้าน]";
			mes "ยังไม่ครบ 10 ชิ้นหรอกนะ!";
			emotion e_no;
			mes "ไปหามาให้ครบก่อนแล้วค่อยกลับมาหาผม!";
			close;
		}
	}

	else if (MQ == 6) {
		mes "[ผู้ใหญ่บ้าน]";
		mes "ขอบใจเจ้ามากๆเลยนะ!!";
		emotion e_kis;
		next;
		mes "[ผู้ใหญ่บ้าน]";
		mes "เนื้อมันอร่อยจริงๆ อะแฮ่ม!! ท่านเทพบอกมาหนะ";
		emotion e_heh;
		next;
		mes "[ผู้ใหญ่บ้าน]";
		mes "เหมือนข้าจะมีงานด่วนเข้าพอดีเลย!! ไว้คุยกับข้าทีหลังนะ!!";
		emotion e_sry;
		close;
	}

	else {
		mes "[ผู้ใหญ่บ้าน]";
		mes "เจ้าดูไม่คุ้นหน้าเลย.....คนเร่ร่อนรึเปล่านิ!";
		emotion e_hmm;
		close;
	}
}