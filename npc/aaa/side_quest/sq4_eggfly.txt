//----------------------------Side Quest 4 เด็กกับไข่ดาว -------------

icecastle,147,85,5	script	Henry ผู้หิวโซ	870,{
	if(SQfour == 2){
		mes "[Henry]";
		mes "ขอบคุณพี่นะ มันอร่อยมากเลย!!";
		emotion ET_THANKS;
		end;
		
		} else if(SQfour == 1){
		mes "[Henry]";
		mes "พี่เอาของมาแล้วใช่ไหม?";
		next;
		if(countitem(574) >= 1 && countitem(7455) >= 1) {
			mes "[Henry]";
			mes "มาเริ่มทำกันเถอะ!! ผมช่วยเตรียมนะ..";
			delitem 574,1;
			delitem 7455,1;
			progressbar_NPC "FF9900", 3;
			set .@rightnum,rand(0,100);
			if(.@rightnum>70){
				specialeffect EF_REFINEOK,AREA;
				mes "เย้!! ไข่ดาวที่ผมอยากกินเสร็จแล้ว!!";
				next;
				mes "[Henry]";
				mes "ขอบคุณพี่นะ ผมไม่มีอะไรจะให้พี่เลย พี่เอาอันนี้ไปละกันนะ!!";
				set .@rightnum,rand(0,100); //สุ่มหินstat 1-2 (70%/30%)|| 4700 4710 4720 4730 4740 4750 (stat+1) /4701 4711 4721 4731 4741 4751 (stat+2)
				if (.@rightnum >70)
					{
						 set .@getstonestat, rand(1, 6);  // Randomize +2 Stat Stone
						 switch(.@getstonestat)
						 {
								case 1: getitem 4701,1; break;
								case 2: getitem 4711,1; break;
								case 3: getitem 4721,1; break;
								case 4: getitem 4731,1; break;
								case 5: getitem 4741,1; break;
								case 6: getitem 4751,1; break;
						 } 
					}
					else 
					{
						set .@getstonestat, rand(1, 6);  // Randomize +1 Stat Stone
						 switch(.@getstonestat)
						 {
								case 1: getitem 4700,1; break;
								case 2: getitem 4710,1; break;
								case 3: getitem 4720,1; break;
								case 4: getitem 4730,1; break;
								case 5: getitem 4740,1; break;
								case 6: getitem 4750,1; break;
						 } 
					}

				specialeffect EF_HEAL2;
				set SQfour,2;
				set SQhidden,1; //ไว้เพิ่มเควสแม่เด็ก คาเรนนนนนนนนนนนนน
				end;
				
			} else { 
				specialeffect EF_CLAYMORE,AREA;
				mes "มันไหม้หมดละอ่าาา..... พี่ทำใหม่ให้ผมที T^T";
				emotion ET_CRY;
				end;
			}
			
		} else { 
			mes "[Henry]";
			mes "เตรียมของเร็วๆนะ ผมหิว...."; 
			mes "";
			mes "^FF0000Side Quest 4";
			end;
		}
		
	} else {
		mes "[Henry]";
		mes "พี่ตรงนั้นหนะ.... ช่วยอะไรผมหน่อยสิ.....";
		mes "ผมหิวข้าวมากเลย.....";
		emotion ET_FOOD;
		mes "";
		mes "^FF0000Side Quest 4";
		next;
		
		switch(select("อยากให้ทำอะไรหละ?","โทษที ยุ่งอยู่")){
			case 1:
				mes "[Henry]";
				mes "ผมอยากกินไข่ดาว.......";
				mes "ไข่เจียวที่......";
				mes "แยกไข่แดงออกมา ทอดไข่ขาวไฟกลาง กรอบนอกนุ่มใน ห้ามมีฟองเลยนะ ราดซอสมะเขือนิดหน่อย";
				next;
				mes "[Henry]";
				mes "แต่ข้าไม่มีวัตถุดิบเลย......";
				mes "พี่ไปหาให้ผมหน่อยนะ";
				mes "ไข่น่าจะอยู่ในป่ามอส ที่มีแม่มดกับแฟรี่อยู่เต็มไปหมด ข้าฟังมาจากนิทานหนะ....";
				mes "ซอสเผ็ดจากที่ร้านข้างทางก็ได้";
				set SQfour,1;
				close;
			
			case 2:
				mes "[Henry]";
				mes "โอย~หิวอ่าาาาา";
				emotion ET_CRY;
				close;
		}
	}
}

//---------------- จบ SQ 4 ----------------------------------

//---------------- Hidden Reward เพื่อรู้ภาษาแมว --------------------

icecastle,157,206,4	script	Karen	918,{

	// ? เคยคุยกับ Kaya แล้ว เข้าใจภาษาแมว
	if (#cat_lang == 1 && SQhidden == 2) {
		mes "[Karen]";
		mes "อืม... ดูเหมือนว่าเธอไปเจอเด็กหูแมวคนนั้นมาแล้วสินะ...";
		emotion ET_THINK;
		next;
		mes "[Karen]";
		mes "พูดอะไรไม่ออกเลยแฮะ... บางทีแมวพวกนั้นก็น่ารักเหมือนกันนะ...";
		emotion ET_SHY;
		close;
	}

	// ? ยังไม่ผ่าน SQfour
	if (SQfour != 2) {
		mes "[Karen]";
		mes "อะไรยะ!? มายืนจ้องหน้าฉันทำไม?";
		emotion ET_FRET;
		next;
		mes "[Karen]";
		mes "คนสมัยนี้ไม่รู้จักมารยาทเลย! จะเข้ามาคุยก็ไม่ยอมแนะนำตัวก่อน!";
		emotion ET_FRET;
		next;
		mes "[Karen]";
		mes "ฉันไม่มีเวลาว่างให้คนแปลกหน้า ถ้าไม่มีธุระจริงๆ ก็ไปซะ!";
		emotion ET_FRET;
		close;
	}

	// ? เคยคุยแล้ว แต่ยังไม่ไปเจอ Kaya
	if (SQhidden >= 2) {
		mes "[Karen]";
		mes "ยังอยู่แถวนี้อีกเหรอ!? ฉันก็บอกที่อยู่เด็กหูแมวไปแล้วนี่!";
		emotion ET_FRET;
		mes "เธอชื่อ Kaya อยู่ที่เกาะอุ้งมือแมวที่ชั้นเคยไปเที่ยว แถวๆกระป๋องยักษ์นั้นแหละ!!";
		mes "อย่าทำตัวน่ารำคาญนักนะ!";
		close;
	}

	// ? ผ่าน SQfour แล้ว ป้ายอมเปิดเผยข้อมูล
	mes "[Karen]";
	mes "หืม... อ๊ะ! นี่เธอนี่นา คนที่ช่วยลูกฉันวันนั้นไว้!";
	next;
	mes "[Karen]";
	mes "จะว่าไปฉันก็ติดหนี้บุญคุณอยู่... ถึงจะไม่อยากยอมรับก็เถอะ!";
	next;
	mes "[Karen]";
	mes "งั้นฟังให้ดีๆ  ชั้นจะไม่บอกซ้ำหรอกนะ!! มีเด็กหูแมวคนหนึ่งชื่อ Kaya... ";
	mes "เธออยู่ที่เกาะอุ้งมือแมวที่ชั้นเคยไปเที่ยวกับสามีชั้น แถวๆกระป๋องปลายักษ์เหม็นๆนั้นแหละ!!";
	next;
	emotion e_swt;
	mes "[Karen]";
	mes "ไม่ใช่เรื่องของฉันหรอก แต่เธออาจอยากไปหา...";
	next;
	mes "[Karen]";
	mes "ถือว่าหนี้เราสองคนเป็นอันจบกันละนะ!";
	specialeffect2 EF_ENHANCE;
	set SQhidden, 2;
	close;
}